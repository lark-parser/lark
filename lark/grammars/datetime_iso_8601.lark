%import common.DIGIT

// Done from a readthrough of the https://en.wikipedia.org/wiki/ISO_8601
// Could be made more robust, to not parse invalid dates

// Date primitives
year  : DIGIT DIGIT DIGIT DIGIT
month : "0" "1".."9"
      | "1" "1".."2" 
day   : "0" DIGIT
      | "1" DIGIT 
      | "2" DIGIT
      | "3" "0".."1" 

// Dates
calendar_date : year "-"? month "-"? day
              | year "-" month
              | "--" month "-"? day

week_number          : "0".."4" DIGIT
                     | "5" "0".."3"
prefixed_week_number : "W" week_number
weekday_number       : "1".."7"
week_date            : year "-"? prefixed_week_number
                     | year "-"? prefixed_week_number "-"? weekday_number

day_number   : "1".."2" DIGIT    DIGIT
             | "3"      "0".."5" DIGIT
             | "3"      "6"      "0".."6" // leap day
ordinal_date : year "-"? day_number

date : ordinal_date 
     | calendar_date 
     | week_date

// Time primitives
hour         : "0".."1" DIGIT
             | "2" "0".."4"
minute       : "0".."5" DIGIT
second       : "0".."5" DIGIT 
             | "60" // leap second
fractional   : "." DIGIT+

// Time
time         : "T"? hour ":"? minute ":"? second fractional?
prefixed_time: "T" hour ":"? minute ":"? second fractional?
time_zone    : "Z"
             | ("+"|"-") hour (":"? minute)

// Combined
datetime     : date prefixed_time time_zone?
